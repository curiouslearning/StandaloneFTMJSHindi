plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'org.curiouslearning.ftmjshindi'
    compileSdk 35

    defaultConfig {
        applicationId "org.curiouslearning.ftmjshindi"
        minSdk 28
        targetSdk 35
        versionCode 3
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def localProps = new Properties()
        def localFile = rootProject.file("local.properties")
        if (localFile.exists()) {
            localFile.withInputStream { localProps.load(it) }
        }

        def fbAppId = localProps.getProperty("FACEBOOK_APP_ID", "")
        def fbClientToken = localProps.getProperty("FACEBOOK_CLIENT_TOKEN", "")

        resValue "string", "facebook_app_id", fbAppId
        resValue "string", "facebook_client_token", fbClientToken
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material

    implementation 'org.nanohttpd:nanohttpd:2.3.1'

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    implementation 'androidx.webkit:webkit:1.9.0'

    // facebook sdk version
    implementation 'com.facebook.android:facebook-android-sdk:18.1.3'
}